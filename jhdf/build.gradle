/*
 * This file is part of jHDF. A pure Java library for accessing HDF5 files.
 *
 * https://jhdf.io
 *
 * Copyright (c) 2025 James Mudd
 *
 * MIT License see 'LICENSE' file
 */

/*
 * Gradle build file for jHDF
 */
plugins {
    // Core plugins
    id 'java-library'
    id 'jacoco' // Code coverage
    id 'maven-publish' // For the artifact collections to publish
    id 'signing' // For GPG signing artifacts
    id 'checkstyle' // Code style checks

    // Community plugins (need versions)
    id 'org.sonarqube' version '4.0.0.2929' // Code quality
    id "com.github.spotbugs" version "6.1.5" // Static analysis
    id "me.champeau.jmh" version "0.7.3" // JMH support
}

// Variables
group = 'io.jhdf'
version = '0.9.1'

compileJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
}

compileTestJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.17.0'
    // lzf compression support
    implementation group: 'com.ning', name: 'compress-lzf', version: '1.1.2'
    // lz5 support https://mvnrepository.com/artifact/org.lz4/lz4-java
    implementation group: 'org.lz4', name: 'lz4-java', version: '1.8.0'

    // Use JUnit 5 test framework
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.11.4'
    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'

    // Mocking
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.11.0'
    testImplementation group: 'org.powermock', name: 'powermock-core', version: '2.0.9'
    // Matchers
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '3.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.3'
    // Alternative bitshuffle impl to check results against
    testImplementation 'org.xerial.snappy:snappy-java:1.1.10.7'
    // For parsing h5dump XML output
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.18.2'
    testImplementation 'commons-io:commons-io:2.18.0'
    // For a FileSystemProvider implementation without FileChannel support
    testImplementation group: 'com.google.jimfs', name: 'jimfs', version: '1.3.0'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

minHeapSize = "128m" // initial heap size
maxHeapSize = "8192m" // maximum heap size
//jvmArgs '-XX:MaxPermSize=5gb' // mem argument for the test JVM

}
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

def getGitHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--verify', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        e.printStackTrace()
        // Can happen if git is unavailable
        return "UNAVALIABLE"
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Automatic-Module-Name': 'io.jhdf',
            // OSGi headers
            'Bundle-ManifestVersion': '2',
            'Bundle-SymbolicName': 'io.jhdf',
            'Bundle-Name': project.name,
            'Bundle-Vendor': 'James Mudd',
            'Bundle-Version': project.version,
            'Export-Package': 'io.jhdf,io.jhdf.*,io.jhdf.api,io.jhdf.api.*',
            'Require-Bundle': buildRequireBundleHeader(),
            // Build data
            'Build-JDK': System.getProperty('java.vendor') + ' ' + System.getProperty('java.version'),
            'Build-OS': System.getProperty('os.name') + ' ' + System.getProperty('os.version'),
            'Git-Commit': getGitHash()
        )
        exclude('io/jhdf/examples')
    }
    from('../CHANGES.md')
    from('../LICENSE')
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'jhdf'
                description = 'A pure Java HDF5 library'
                url = 'https://jhdf.io/'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'jamesmudd'
                        name = 'James Mudd'
                        email = 'james.mudd@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/jamesmudd/jhdf.git'
                    developerConnection = 'scm:git:git://github.com/jamesmudd/jhdf.git'
                    url = 'https://github.com/jamesmudd/jhdf.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/jamesmudd/jhdf/issues'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'GitHub'
            url = 'https://maven.pkg.github.com/jamesmudd/jhdf'
            credentials {
                username = 'jamesmudd'
                password = System.getenv('GITHUB_TOKEN')
            }
        }
        maven {
            name = 'Sonatype'
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}

signing {
    required { System.getenv("SIGNING_KEY") != null }
    def signingKey = System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}

import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort

import java.util.stream.Collectors

spotbugs {
    ignoreFailures = true // Allow build to continue with errors
    effort = Effort.valueOf('MAX')
    reportLevel = Confidence.valueOf('LOW') // Report all issues even low priority
    reportsDir = file("$buildDir/reports/spotbugs")
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "jamesmudd-github"
        property "sonar.projectKey", "jamesmudd_jhdf"
        // Exclusions
        property "sonar.exclusions", "src/main/java/io/jhdf/examples"
        // Spotbugs
        property "sonar.java.spotbugs.reportPaths", "build/reports/spotbugs/main.xml,build/reports/spotbugs/test.xml"
        property "sonar.jacoco.reportPath", null
        property "sonar.jacoco.reportPaths", null
        // Coverage
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
        // Checkstyle
        property "sonar.java.checkstyle.reportPaths", "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
    }
}

String buildRequireBundleHeader() {
    def runtimeDependencies = configurations.runtimeClasspath.getAllDependencies()
    def bundleToModule = [
        "slf4j.api": "org.slf4j:slf4j-api",
        "org.apache.commons.lang3": "org.apache.commons:commons-lang3",
        "com.ning.compress-lzf": "com.ning:compress-lzf",
        "lz4-java": "org.lz4:lz4-java"
    ]

    def moduleToVersion = runtimeDependencies.collectEntries {
        [it.module.toString(), it.version]
    }

    //  Check here to catch when dependencies are added/removed
    if(!bundleToModule.values().containsAll(moduleToVersion.keySet())) {
        throw new IllegalStateException("Runtime dependencies not mapped for OSGi")
    }

    def header = bundleToModule.entrySet().stream()
        .map {"${it.key};bundle-version=\"${moduleToVersion.get(it.value)}\"" }
        .collect(Collectors.joining(","))

    return header
}
